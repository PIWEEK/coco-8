( sprite animation )
|10 @Screen &vector $2 &x $1 &y $1 &pixel $1 &pad $3 &address $2 &sprite $1 

%MOD { DUP2 DIV MUL SUB } ( a b -- a%b )

|0000
@x $1
@t $1
@pacman-spr $2

|0100 ( -> )
  ;on-frame .Screen/vector DEO2

  ( init vars )
  ;pacman00 .pacman-spr STZ2
  #00 .t STZ
  #00 .x STZ 

  ( draw bg )
  #21 .Screen/pixel DEO
BRK

@on-frame ( -> )
  ( clear foreground )
  #00 .Screen/x DEO
  #00 .Screen/y DEO
  #30 .Screen/pixel DEO
  
  ( move party )
  .x LDZ INC #c0 MOD DUP

  .x STZ .Screen/x DEO

( handle animation time )
  .t LDZ INC DUP .t STZ #0f EQU ,&update-frame JCN ,&draw-pacman JMP
  &update-frame
    #00 .t STZ
    .pacman-spr LDZ2 ;pacman00 EQU2 ,&change-to-01 JCN ,&change-to-00 JMP
  &change-to-01
    ;pacman01 .pacman-spr STZ2 ,&draw-pacman JMP
  &change-to-00
    ;pacman00 .pacman-spr STZ2

  ( draw pacman )
  &draw-pacman
    #44 .Screen/y DEO
    .pacman-spr LDZ2 .Screen/address DEO2
    #10 .Screen/sprite DEO

  ( draw ghost )
  &draw-ghost
    ;coco .Screen/address DEO2
    .x LDZ #0f ADD #c0 MOD .Screen/x DEO
    #10 .Screen/sprite DEO
BRK

@coco
0077 7700
0777 7770
6717 7176
7f77 77f7
7711 1177
7772 8777
7777 7777
7607 7067

@pacman00
00aa aa00
0aa1 8aa0
9aaa a000
9aa0 0000
99a0 0000
99aa a000
0999 aaa0
0099 9900

@pacman01
00aa aa00
0aaa aaa0
9aaa 18aa
9aaa aaaa
99aa aaaa
99aa aaaa
0999 aaa0
0099 9900